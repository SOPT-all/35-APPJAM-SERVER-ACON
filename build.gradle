plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '7.0.1'
}

group = 'com.acon'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Web
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Database
    implementation 'org.postgresql:postgresql:42.7.4'

    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    // Google
    implementation 'com.google.api-client:google-api-client:2.7.0'
    // JWT
    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
    implementation group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
    implementation group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('updateGitHooks', Copy) {
    from './scripts'
    into './.git/hooks'

    filesMatching(['prepare-commit-msg', 'pre-commit']) {
        mode = 0775 // 실행 권한 부여
    }
}

tasks.named('build') {
    dependsOn 'updateGitHooks'
}

spotless {
    java {
        target '**/*.java'
        eclipse().configFile file('eclipse-formatter.xml')
        importOrder()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }

    format('misc') {
        target '**/*.gradle', '**/*.gitignore'
        trimTrailingWhitespace()
        endWithNewline()
    }
}
